

:::::::::::::::::::::::::::::::::::::::::::::
:: Ran every time copies-backup is called.
:::::::::::::::::::::::::::::::::::::::::::::
:: setup 7.9.5.50 20221013082341 by -JpE-
:::::::::::::::::::::::::::::::::::::::::::::
::set ver=v7.9.8.77 20221114032913 by -JpE-
:::::::::::::::::::::::::::::::::::::::::::::

::: RC2
title %ttl1%  [Get Wipe Prefs] %ttl2%
set /a local1=0
set /a rural1=0
set /a extern=0
if exist "I:\" set /a extern+=1
if exist "X:\" set /a extern+=2
if exist "H:\" set /a extern+=4
:: NOTE: extern= (1 is just I, 2 is just X, 3 is both.
::  4 is just H, 5 is H & I, 6 is H & X, 7 is all.

:: rev93 introduces the r3v3rs3 var and replaces
:: recovery modules with the mods to copies & get modules.
echo         %vers%
echo .
echo     ***  Processing: %oper%  ***
echo .
if %r3v3rs3% equ 2 set /a extern=0
if %r3v3rs3% equ 2 goto ifrestore

if %extern% equ 0 echo %sl%
if %extern% equ 0 echo           **** No EXTERNAL Drives are Online. ****
if %extern% equ 0 echo %sl%
if %extern% equ 0 echo .


:ifrestore
set txt="Backups"
if %r3v3rs3% equ 2 set txt="RESTORE"
set msg="==> Wipe Destination for [ Local ] %txt%? (y/N/c): "
choice /c ync /n /t 7 /d n /m %msg%
if %errorlevel% equ 3 goto cancel
set /a local1=%errorlevel%
:: local1 is now either: 1==y or 2==n
echo .
if %extern% equ 0 goto calc
:: No choices here, move along, move along... 

echo %l1%
echo .
echo     ***  Processing: %oper%  ***
echo .
:: 
set msg="Wipe Destinations for [ External ] Backups? (y/N/c): "
choice /c ync /n /t 7 /d n /m %msg%
if %errorlevel% equ 3 goto cancel
set /a rural1=%errorlevel%
:: rural1 is now either: 1==y or 2==n, 0 if no externals.
echo .


:calc
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: NOTE: [choices] for Wipe, defaults to 2 == no; BE CAREFUL!::
:: 0 == Neither; 1 == Local Only; 2 == Rural Only,           ::
:: 3 == Both, 4 == Cancel. Each is either 1 or 2 ONLY.       ::
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: NOTE: :: Rev8888 introduces var; RE: extern
:: if %extern% equ 0 set /a rural1=0 is implicite Yes?
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
set /a wipe=0
if %local1% equ 1 set /a wipe=1
if %rural1% equ 1 set /a wipe=2
if %local1% equ 1 if %rural1% equ 1 set /a wipe=3
:: Yes, yes it is implied above, when 2nd choice gets skipped.
goto return


:cancel
echo .
echo %sl%
echo              **** ACTION CANCELLED BY USER ****
echo %sl%
echo .
set /a wipe=4
set /a exit2=4


:return
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: DOCS for repeated uses:
:: if cancel (Wipe==4) it's handled already;
:: if neither it passes through with Wipe==0;
:: if wipe==1 just do local,
:: if wipe==2 just do rural, if 3 do both.
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::



:: 100% -JpE-



